plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'no.unit.nva'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8  // source-code version and must be <= targetCompatibility
targetCompatibility = 1.8 // bytecode target version

repositories {
    mavenCentral()
}

def powermock_version = '2.0.4'

dependencies {
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.29'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.12.1'
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.0'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0-M1'
    testImplementation 'org.mockito:mockito-core:2.1.0'
    testImplementation "org.powermock:powermock-module-junit4:${powermock_version}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermock_version}"
    testImplementation 'commons-io:commons-io:2.6'

    shadowJar {
        archiveClassifier.set('')
        zip64 true
    }
}


test {
    failFast = true
    finalizedBy jacocoTestReport
}

// We don't want a jar, just a fatJar
jar.enabled = false
project.tasks.build.dependsOn project.tasks.shadowJar

pmd {
    toolVersion = '6.20.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco")
    }
}

jacoco {
    toolVersion = "0.8.5"
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1
            }
        }
    }
}
